// ContactPassword.DG

Use DfAllEnt.pkg
Use Contact.DD
Use Customer.DD
Use SalesRep.DD
Use cSnowrepDataDictionary.dd
Use ContactWebUserProcess.bp

Object oContactPasswordDialog is a cGlblDbModalPanel
    Set Border_Style to Border_Thick
    Set Label to "Change Password"
    Set Size to 102 238
    Set Location to 26 79
    Set piMinSize to 102 238
    Set Locate_Mode to Center_On_Panel

    Object oSnowrep_DD is a cSnowrepDataDictionary
    End_Object

    Object oSalesrep_DD is a Salesrep_DataDictionary
    End_Object

    Object oCustomer_DD is a Customer_DataDictionary
    End_Object

    Object Contact_DD is a Contact_DataDictionary
        Set DDO_Server to oSnowrep_DD
        Set DDO_Server to oSalesrep_DD
        Set DDO_Server to oCustomer_DD
    End_Object    // Contact_DD

    Set Main_DD to Contact_DD
    Set Server to Contact_DD

    Object oPasswordGroup is a cGlblDbGroup
        Set Size to 75 228
        Set Location to 0 5

        Object oContact_LoginName is a cGlblDbForm
            Entry_Item Contact.WebLoginID
            Set Label to "Login Name"
            Set Size to 13 126
            Set Location to 10 66
            Set Enabled_State to False
        End_Object

        Object oContact_Password is a cGlblDbForm
            Entry_Item Contact.WebLoginPW
            Set Label to "Current Password"
            Set Size to 13 126
            Set Location to 25 66
            Set Password_State to True
            Set Enabled_State to False
        End_Object

        Object oPasswordForm is a cGlblForm
            Set Label to "Type Password"
            Set Size to 13 126
            Set Location to 40 66
            Set Password_State to True

            On_Key kCancel Send Close_Panel
        End_Object    // oPasswordForm

        Object oVerifyForm is a cGlblForm
            Set Label to "Verify Password"
            Set Size to 13 126
            Set Location to 55 66
            Set Password_State to True

            On_Key kCancel Send Close_Panel
            On_Key kEnter  Send DoSavePassword
        End_Object    // oVerifyForm

    End_Object    // oPasswordGroup

    Object oOK_bn is a Button
        Set Label to "&OK"
        Set Location to 80 125
        Set peAnchors to anBottomRight

        procedure OnClick
            send DoSavePassword
        end_procedure
    End_Object    // oOK_bn

    Object oCancel_bn is a Button
        Set Label to "&Cancel"
        Set Location to 80 180
        Set peAnchors to anBottomRight

        procedure OnClick
            send Close_Panel
        end_procedure
    End_Object    // oCancel_bn

    procedure DoSavePassword
        Boolean bFail
        Integer iUserId
        Handle  hoDD
        String  sPassword sVerify sEncrypted
        //
        get Value of oPasswordForm to sPassword
        get Value of oVerifyForm   to sVerify
        //
        if (sPassword <> sVerify) begin
            send Stop_Box "Password does not match verification"
            procedure_return
        end
        //
        if (sPassword <> "") begin
            get Server to hoDD
            get IsEncryptedPassword sPassword to sEncrypted
            Set Field_Changed_Value of hoDD Field Contact.WebLoginPW to sPassword // sEncrypted
            Send Request_Save       of hoDD
            If (not(Changed_State(hoDD))) Begin
                Move Contact.WebUserID to iUserId
                //
                Get DoUpdateWebUser of oContactWebUserProcess ;
                    (&iUserId)          ;
                    Contact.ContactIdno ;
                    Contact.WebLoginID  ;
                    Contact.WebLoginPW  ;
                    Contact.FirstName   ;
                    Contact.LastName to bFail
                If (not(bFail) and iUserId <> Contact.WebUserID) Begin
                    Set Field_Changed_Value of hoDD Field Contact.WebUserID to iUserId
                    Send Request_Save       of hoDD
                End
            End
        End
        //
        send Close_Panel
    end_procedure

    procedure DoChangePassword integer iRecnum
        handle hoDD
        //
        set Value of oPasswordForm to ""
        set Value of oVerifyForm   to ""
        //
        get Server to hoDD
        send Clear of hoDD
        Send Find_By_Recnum of hoDD Contact.File_number iRecnum
        send Popup
    end_procedure

End_Object    // oContactPasswordDialog
