//AB/ Project Estimating Standard Link Audit
//AB/ Object prj is a Report_Project
    //AB/ Set Size to 554 856
    //AB/ Set ProjectName to "Estimating Standard Link Audit"
    //AB/ Set ProjectFileName to "esa070l2.rv"

// Project Object Structure
//   Esa070L2_rpt is a ReportView
//     oSelectGroup1 is a Group
//       oSelStart1 is a Form
//       oSelStop1 is a Form
//       oFilterCode is a Form
//     oPrintTo is a RadioGroup
//       oRadio1 is a Radio
//       oRadio2 is a Radio
//     oBtnPrint is a Button
//     oBtnCancel is a Button
//     RptEsa070L2 is a vpe_Winreport

// Register all objects
Register_Object Esa070L2_rpt
Register_Object oBtnCancel
Register_Object oBtnPrint
Register_Object oFilterCode
Register_Object oPrintTo
Register_Object oRadio1
Register_Object oRadio2
Register_Object oSelectGroup1
Register_Object oSelStart1
Register_Object oSelStop1
Register_Object RptEsa070L2


//AB-StoreTopStart

Use DFAllRpt.pkg
Use VWinRpt.pkg

Open Jcoper
Open Jccntr


//AB-StoreTopEnd

//AB-IgnoreStart
Use dfrptvw.pkg
Use Windows.pkg
Use dfRadio.pkg
//AB-IgnoreEnd


ACTIVATE_VIEW Activate_Esa070L2_rpt FOR Esa070L2_rpt

Object Esa070L2_rpt is a ReportView

    //AB-StoreTopStart
    set Icon to "Winprint.ico"
    property string psFilterCode ""
    //set Locate_Mode to CENTER_ON_PANEL
    //    Use Est_Std.rv
    //AB-StoreTopEnd

    Set Label to "Est. Std. Link Audit Listing"
    Set Location to 6 6
    // Visual DataFlex 14.0 Client Size Adjuster, modified May 23, 2008: 14:36:25
//    Set Size to 120 182
    Set Size to 105 178

    //AB-DDOStart


    //AB-DDOEnd

    Object oSelectGroup1 is a Group
        Set Size to 49 165
        Set Location to 5 5
        Set Label to "Select Standard Operation and Link Symbol"
        Object oSelStart1 is a Form
            Set Label to "Operation #"
            Set Size to 13 40
            Set Location to 13 48
            Set Status_Help to "Standard Operation No. to audit."
            Set Label_Col_Offset to 2
            Set Label_Justification_Mode to jMode_Right
        End_Object    // oSelStart1

        Object oSelStop1 is a Form
            Set Label to "Link Symbol:"
            Set Size to 13 15
            Set Location to 13 142
            Set Status_Help to "Link Symbol to audit."
            Set Label_Col_Offset to 2
            Set Label_Justification_Mode to jMode_Right

            //AB-StoreStart
            set capslock_state to true
            //AB-StoreEnd

        End_Object    // oSelStop1

        Object oFilterCode is a Form

            //AB-StoreTopStart
            set Capslock_State to true
            //AB-StoreTopEnd

            Set Label to "CC Filter Code"
            Set Size to 13 18
            Set Location to 30 70
            Set Status_Help to "Cost Centers selected must have matching filter code. If blank, all cost centers will be output."
            Set Label_Col_Offset to 2
            Set Label_Justification_Mode to jMode_Right

            //AB-StoreStart
            set form_mask to capslock
            //Procedure OnChange
            //    String sValue
            //    Get value to sValue
            //
            //End_Procedure  // OnChange
            //AB-StoreEnd

        End_Object    // oFilterCode

    End_Object    // oSelectGroup1

    Object oPrintTo is a RadioGroup
        Set Size to 38 105
        Set Location to 58 5
        Set Label to "Print to"
        Object oRadio1 is a Radio
            Set Label to "Preview"
            Set Size to 10 42
            Set Location to 12 6
            Set Status_Help to "Prints the report to screen"
        End_Object    // oRadio1

        Object oRadio2 is a Radio
            Set Label to "Printer"
            Set Size to 10 42
            Set Location to 24 6
            Set Status_Help to "Prints the report to printer"
        End_Object    // oRadio2

    End_Object    // oPrintTo

    Object oBtnPrint is a Button
        Set Label to "Print"
        Set Location to 64 120
        Set Status_Help to "Print the Selected report"
        Set Default_State to TRUE

        //AB-StoreStart
        Procedure OnClick
            Send StartReport
        End_Procedure
        //AB-StoreEnd

    End_Object    // oBtnPrint

    Object oBtnCancel is a Button
        Set Label to "Cancel"
        Set Location to 82 120
        Set Status_Help to "Close this Panel"

        //AB-StoreStart
        Procedure OnClick
            Send Close_Panel
        End_Procedure
        //AB-StoreEnd

    End_Object    // oBtnCancel


    //AB-StoreStart
    // Procedures and functions used by the user interface
    
    //This function is called by the procedure StartReport
    Function Print_to_Screen_State Returns Integer
        Local Integer iRad
        Get Current_Radio of (oPrintTo(Self)) To iRad
        Function_Return (iRad=1)
    End_Function
    
    // Use this procedure to do print setup
    Procedure SetupReport
        send DFPrintSetup to (Report_Object_Id(Self))
    End_Procedure
    
    
    // The Report
    Object RptEsa070L2 is a vpe_Winreport
        Property Integer piOpcode 0
        Property String  psLinkSymbol ""

        //Set the title on the information box (sentinel)
        Set Report_Title to "Printing Est. Std. Link Symbol Audit"
    
        //Main File for the report
        Report_Main_File JCOPER
    
        // Constrain to use depending on users choice
        // This is a smart constraint. If either start or
        // stop is blank we will assume that there is no limit
        Begin_Constraints
            string sFilterCode
            get psFilterCode to sFilterCode
            constrain Jcoper.Est_std eq "Y"
            if (not(sFilterCode="")) constrain Jccntr.Picklist_Code EQ sFilterCode
        End_Constraints
    
        //Breaks and Index for the report
        Report_Breaks JCOPER.OPCODE
        Report_Index By Index.2
    
        // Starting_Main_Report should ALWAYS be used!
        // Setting of margins should be set inside
        // Starting_Main_Report and after the Forward Get.
        Function Starting_Main_Report Returns Integer
            Local Integer iRetVal
            Send DFSetMetrics To WinPrintId "INCH" // Must Set Before Forward!
            Forward Get Starting_Main_Report To iRetVal
            If iRetVal Function_return iRetVal
            send DFSetmargins       of WinPrintId 0.5 0.5 0.5 0.5
            send DFSetSpecPrintFlag of WinPrintId DF_PAPER_LETTER
        End_Function
    
        Procedure_Section Page_Top
            string sStart sStop sSymbolDesc sTitle sFilterCode
            date dToday
            integer iHr iMn iOpcode iPosition
            string sDateTime sHr sMn sLinkSymbol
            number nCol1 nCol2 nCol3 nCol4 nCol5 nCol6
            Move 0.5  to nCol1
            Move 0.75 to nCol2
            Move 1.75 to nCol3
            Move 4.00 to nCol4
            Move 4.50 to nCol5
            Move 6.00 to nCol6
    
            sysdate dToday iHr iMn
            move "" to sHr
            if iHr lt 10 append sHr "0" iHr
            else move iHr to sHr
            move "" to sMn
            if iMn lt 10 append sMn "0" iMn
            else move iMn to sMn
            move "" to sDateTime
            append sDateTime dToday "  " sHr ":" sMn
    
            Get piOpcode to iOpcode
            Get psLinkSymbol to sLinkSymbol
            get psFilterCode to sFilterCode
    
            DFFont "Arial"
    
              DFBeginHeader DFPageTop          //Header of type DFPageTop
    
                DFHeaderPos   HDR_LEFT         //Write data from left margin in the header
                DFHeaderFrame HDR_MARGINS 0.02 //Set the box from margin to margin using size 0.02
    
                DfFontSize 2
                DfWriteLn
                DFFontSize 10
                DFWritePos sDateTime       0.2     (FONT_DEFAULT) -1 0
                DFFontSize 12
                DFWritePos "Estimating Standard Link Symbol Audit"  4 (FONT_CENTER+FONT_ITALIC+FONT_BOLD) -1
                DFFontSize 10
                DFWritePos "Page"         6.75
                DFWritePos "#pagecount#"  7.25//(FONT_RIGHT)
                DFWriteln ""
                if (sFilterCode > "") ; 
                    Dfwritepos ("Filter Code="*sFilterCode) 0.2 (Font_Bold) -1
                DFWriteln ""
                //
                DFFontSize 9
                // ??? need to set properties and find record and set properties BEFORE the report executes
                // this seems to work, but I wonder about a two page report losing track of where it is
                clear jcoper
                move iOpcode to Jcoper.Opcode
                Find eq Jcoper.Opcode
                Relate Jcoper
                //
                If (sLinkSymbol = "S") Move "OP'S STD. VALUE"  to sSymbolDesc
                If (sLinkSymbol = "T") Move JCOPER.Table_Std_Desc         to sSymbolDesc
                If (sLinkSymbol = "" ) Move JCOPER.Calc_Std_Desc          to sSymbolDesc
                If (sLinkSymbol = "0") Move JCOPER.Calc_Units_Desc        to sSymbolDesc
                Move (Pos(sLinkSymbol,"123456789")) to iPosition
                If iPosition Begin
                    Move (iPosition -1) to Fieldindex
                    Move JCOPER.Var1_Tag& to sSymbolDesc
                End
                Move (Pos(sLinkSymbol,"ABCDEFGHI")) to iPosition
                If iPosition Begin
                    Move (iPosition -1) to Fieldindex
                    Move JCOPER.Con1_Desc& to sSymbolDesc
                End
                If (sLinkSymbol = "" ) Move "' '" to sLinkSymbol
                Move ("Variables & Constants Linked to CC|OP" * string(iOpcode) +": " + trim(Jccntr.Nickname) + "|" -Jcoper.NickName + ", Symbol" * sLinkSymbol +":" * sSymbolDesc) ; 
                     to sTitle
                //
                clear Jcoper
                //restore buffer???? NOPE: works fine, tested 2-page report, no recs skipped, header correct
                //
                dfWriteLn sTitle (FONT_BOLD+FONT_CENTER)
                DFWriteln ""
                DFFontSize 9
                DFWritePos "Opcode"         nCol1 (FONT_BOLD+FONT_RIGHT) -1 0.6
                DFWritePos "Cost Center"    nCol2 (FONT_BOLD)            -1 1.9
                DFWritePos "Operation"      nCol3 (FONT_BOLD)            -1 3.0
                DFWritePos "Var./Con."     (nCol4 - 0.2) (FONT_BOLD)            -1 3.0
                DFWritePos "Description"    nCol5 (FONT_BOLD)            -1 3.0
                DFWritePos "Var. Modifiers" nCol6 (FONT_BOLD)            -1 3.0
                DfFontSize 2
                DfWriteLn
                DfWriteLn
    
                // Remove comment to display a metric ruler
                //Send ShowRuler // displays a ruler for aligning fields
              DFEndHeader                     //End and print header
                //DFWriteln ""                  // Doing this looks better
                //DFWriteln ""                  // Doing this looks better
    
        End_Procedure
    
        Procedure_Section Body
            boolean bSelect
            integer iOpcode
            string  sLinkSymbol
            number nCol1 nCol2 nCol3 nCol4 nCol5 nCol6
            Move 0.5  to nCol1
            Move 0.75 to nCol2
            Move 1.75 to nCol3
            Move 4.00 to nCol4
            Move 4.50 to nCol5
            Move 6.00 to nCol6
            Get piOpcode     to iOpcode
            Get psLinkSymbol to sLinkSymbol
            // Update the information box (sentinel) for each body record
            Send Update_Status ("Opcode" * string(JCOPER.Opcode))
            DFFontSize 8
            //
            For Fieldindex from 0 to 8
                Move (JCOPER.Var1_Link_Code& EQ iOpcode) to bSelect
                if bSelect Move (JCOPER.Var1_Link_Symbl& = sLinkSymbol) to bSelect
                if bSelect Begin
                    Relate JCOPER
                    DFWritePos Jcoper.OPCODE          nCol1 (FONT_DEFAULT+FONT_RIGHT) -1 0.62
                    DFWritePos Jccntr.NickName        nCol2 (FONT_DEFAULT)            -1 1.9
                    DFWritePos Jcoper.NAME            nCol3 (FONT_DEFAULT)            -1 3.0
                    DFWritePos (Fieldindex +1)        ncol4
                    DfWritePos JCOPER.Var1_Tag&       nCol5
                    DfWritePos JCOPER.Var1_Tag_Symbl& nCol6
                    DFWriteLine DFGR_CURRLINE 0 DFGR_RB_MARGIN DFGR_HORI RGB_BLACK 0.01
                    DfWriteLn ""
                end
            Loop
            For Fieldindex from 0 to 8
                Move (JCOPER.Con1_Link_Code& EQ iOpcode) to bSelect
                if bSelect Move (JCOPER.Con1_Link_Symbl& = sLinkSymbol) to bSelect
                if bSelect Begin
                    Relate JCOPER
                    DFWritePos Jcoper.OPCODE     nCol1 (FONT_DEFAULT+FONT_RIGHT) -1 0.62
                    DFWritePos Jccntr.NickName   nCol2 (FONT_DEFAULT)            -1 1.9
                    DFWritePos Jcoper.NAME       nCol3 (FONT_DEFAULT)            -1 3.0
                    DfWritePos (Mid("ABCDEFGHI",1,(Fieldindex+1))) ; 
                                                 nCol4
                    DfWritePos JCOPER.Con1_Desc& nCol5
                    DFWriteLine DFGR_CURRLINE 0 DFGR_RB_MARGIN DFGR_HORI RGB_BLACK 0.01
                    DfWriteLn ""
                end
            Loop
            For Windowindex from 0 to 2
                Move (Windowindex *3) to Fieldindex
                Move (JCOPER.C1_Link_Code& EQ iOpcode) to bSelect
                if bSelect move (JCOPER.C1_Link_Symbl& = sLinkSymbol) to bSelect
                if bSelect Begin
                    Relate JCOPER
                    DFWritePos Jcoper.OPCODE                nCol1 (FONT_DEFAULT+FONT_RIGHT) -1 0.62
                    DFWritePos Jccntr.NickName              nCol2 (FONT_DEFAULT)            -1 1.9
                    DFWritePos Jcoper.NAME                  nCol3 (FONT_DEFAULT)            -1 3.0
                    DfWritePos Jcoper.C1_Tag_Symbl&         nCol4
                    If (Jcoper.C1_Tag_Symbl&="0") ; 
                      DfWritePos Jcoper.Calc_Units_Desc     nCol5
                    Else if (Jcoper.C1_Tag_Symbl&="P") ; 
                      DfWritePos Jcoper.Calc_Std_Desc       nCol5
                         Else ; 
                           DfWritePos Jcoper.Table_Std_Desc nCol5
                    DFWriteLine DFGR_CURRLINE 0 DFGR_RB_MARGIN DFGR_HORI RGB_BLACK 0.01
                    DfWriteLn ""
                end
            Loop
            //
        End_Procedure
    
    
    End_Object    //  RptEsa070L2
    
    // Use this procedure to start the report
    Procedure StartReport
        Integer iToPrinter iRepObj
        String sLinkSymbol sSetupFile sFilterCode
        Integer iOpcode
        String nStart nStop
        Get Report_Object_Id To iRepObj
    
        Get Value of (oSelStart1(oSelectGroup1(Self))) item 0 To iOpcode
        Set piOpcode of iRepObj To iOpcode
    
        Get Value of (oSelStop1(oSelectGroup1(Self))) item 0 To sLinkSymbol
        Set psLinkSymbol of iRepObj To sLinkSymbol
    
        get value of oFilterCode to sFilterCode
        set psFilterCode to sFilterCode

        Get Print_to_Screen_State to iToPrinter
        If iToPrinter Set OutPut_Device_Mode to PRINT_TO_PRINTER
        Else Set OutPut_Device_Mode to PRINT_TO_WINDOW
    
        // set the VPE setup file
        get_environment "TEMP" to sSetupFile
        move (trim(sSetupFile) + "\Vpe.prs") to sSetupFile
        set pSetupFile   of oVpePrint to sSetupFile
        //
        set pCaption     of oVpePrint to "Estimating Standards Link Symbol Audit"
        set pVpeDocFlags of oVpePrint to (VPE_NO_RULERS+VPE_NO_MAILBUTTON)
        //
        Send Run_Report to iRepObj
    End_Procedure
    
    On_Key Key_Escape Send close_panel
    
    //AB-StoreEnd

End_Object    // Esa070L2_rpt

//AB/ End_Object    // prj

