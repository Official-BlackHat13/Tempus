// ChangePassword.DG

Use DfAllEnt.pkg
Use User.DD

Object oChangePasswordDialog is a cGlblDbModalPanel
    Set Border_Style to Border_Thick
    Set Label to "Change Password"
    Set Size to 102 238
    Set Location to 26 79
    Set piMinSize to 102 238

    Object User_DD is a User_DataDictionary
    End_Object    // User_DD

    Set Main_DD to User_DD
    Set Server to User_DD

    Object oPasswordGroup is a cGlblDbGroup
        Set Size to 75 228
        Set Location to 0 5

        Object oUser_LoginName is a cGlblDbForm
            Entry_Item User.LoginName
            Set Label to "Login Name"
            Set Size to 13 126
            Set Location to 10 66
            Set Enabled_State to False
        End_Object

        Object oUser_Password is a cGlblDbForm
            Entry_Item User.Password
            Set Label to "Current Password"
            Set Size to 13 126
            Set Location to 25 66
            Set Password_State to True
            Set Enabled_State to False
        End_Object

        Object oPasswordForm is a cGlblForm
            Set Label to "Type Password"
            Set Size to 13 126
            Set Location to 40 66
            Set Password_State to True

            On_Key kCancel Send Close_Panel
        End_Object    // oPasswordForm

        Object oVerifyForm is a cGlblForm
            Set Label to "Verify Password"
            Set Size to 13 126
            Set Location to 55 66
            Set Password_State to True

            On_Key kCancel Send Close_Panel
            On_Key kEnter  Send DoSavePassword
        End_Object    // oVerifyForm

    End_Object    // oPasswordGroup

    Object oOK_bn is a Button
        Set Label to "&OK"
        Set Location to 80 125
        Set peAnchors to anBottomRight

        procedure OnClick
            send DoSavePassword
        end_procedure
    End_Object    // oOK_bn

    Object oCancel_bn is a Button
        Set Label to "&Cancel"
        Set Location to 80 180
        Set peAnchors to anBottomRight

        procedure OnClick
            send Close_Panel
        end_procedure
    End_Object    // oCancel_bn


    //AB-StoreStart
    procedure DoSavePassword
        handle hoDD
        string sPassword sVerify sEncrypted
        //
    //    get Value of (oPasswordForm(oPasswordGroup(self))) to sPassword
    //    get Value of (oVerifyForm  (oPasswordGroup(self))) to sVerify
        get Value of oPasswordForm to sPassword
        get Value of oVerifyForm   to sVerify
        //
        if (sPassword <> sVerify) begin
            send Stop_Box "Password does not match verification"
            procedure_return
        end
        //
        if (sPassword <> "") begin
            get Server to hoDD
            get IsEncryptedPassword sPassword to sEncrypted
            Set Field_Changed_Value of hoDD Field User.User_password to sEncrypted
            send Request_Save of hoDD
        end
        //
        send Close_Panel
    end_procedure

    procedure DoChangePassword integer iRecnum
        handle hoDD
        //
        set Value of oPasswordForm to ""
        set Value of oVerifyForm   to ""
        //
        get Server to hoDD
        send Clear of hoDD
        Send Find_By_Recnum of hoDD User.File_number iRecnum
        send Popup
    end_procedure

End_Object    // oChangePasswordDialog
