// OpenTransactions.wo
// Open Transactions
Use cWebBusinessProcess.pkg
Use cWebReport.pkg

Use Employer.DD
Use Employee.DD
Use Customer.DD
Use Areas.DD
Use Location.DD
Use MastOps.DD
Use Opers.DD
Use Order.DD
Use Trans.DD
Use User.DD
Use cWorkTypeGlblDataDictionary.dd
Use cSalesTaxGroupGlblDataDictionary.dd


Object oOpenTransactions Is A cWebBusinessProcess
    Set psDescription To "Open Transactions"

    Object oSalesTaxGroup_DD is a cSalesTaxGroupGlblDataDictionary
    End_Object

    Object oWorkType_DD is a cWorkTypeGlblDataDictionary
    End_Object

    Object oUser_DD is a User_DataDictionary
    End_Object


    Object oEmployer_DD Is A Employer_DataDictionary
        Send DefineAllExtendedFields
    End_Object // oEmployer_DD

    Object oEmployee_DD Is A Employee_DataDictionary
        Send DefineAllExtendedFields
        Set DDO_Server To oEmployer_DD
    End_Object // oEmployee_DD

    Object oCustomer_DD Is A Customer_DataDictionary
        Send DefineAllExtendedFields
    End_Object // oCustomer_DD

    Object oAreas_DD Is A Areas_DataDictionary
        Send DefineAllExtendedFields
    End_Object // oAreas_DD

    Object oLocation_DD Is A Location_DataDictionary
        Set DDO_Server to oSalesTaxGroup_DD
        Send DefineAllExtendedFields
        Set DDO_Server To oCustomer_DD
        Set DDO_Server To oAreas_DD
    End_Object // oLocation_DD

    Object oMastops_DD Is A Mastops_DataDictionary
        Set DDO_Server to oWorkType_DD
        Send DefineAllExtendedFields
    End_Object // oMastops_DD

    Object oOpers_DD Is A Opers_DataDictionary
        Send DefineAllExtendedFields
        Set DDO_Server To oLocation_DD
        Set DDO_Server To oMastops_DD
    End_Object // oOpers_DD

    Object oOrder_DD Is A Order_DataDictionary
        Send DefineAllExtendedFields
        Set DDO_Server To oCustomer_DD
        Set DDO_Server To oLocation_DD
    End_Object // oOrder_DD

    Object oTrans_DD Is A Trans_DataDictionary
        Send DefineAllExtendedFields
        Set DDO_Server To oEmployee_DD
        Set DDO_Server To oCustomer_DD
        Set DDO_Server To oLocation_DD
        Set DDO_Server To oOrder_DD
        Set DDO_Server To oOpers_DD

        Property Number pnStartAreanumber
        Property Number pnStopAreanumber
        
        Property Number pnEmployer
        Property Number pnJobNo

        Procedure OnConstrain 
            Number nStart nStop nEmployer nJobNo
            String sUsername

            Get pnStartAreanumber   to nStart
            Get pnStopAreanumber    to nStop
            Get pnEmployer          to nEmployer
            Get pnJobNo             to nJobNo
            
            Constrain Trans.InvoicedFlag eq 0
            Constrain Trans.StopHr       eq 0
            Constrain Trans.StopMin      eq 0
            Constrain Trans.StopDate     eq 0
            //OJ Nanny exclusion
            Constrain Trans.EmployeeIdno ne 2433
            
            If (nJobNo <> 0) Begin
            Constrain Trans.JobNumber eq nJobNo
            End
            
            If (nEmployer <> 0) Begin
            Constrain Trans.EmployerIdno eq nEmployer
            End
            
            If (nStart <> 0 and nStop <> 0) Begin
                Constrain Trans.Areanumber Between nStart And nStop
            End
            Else Begin
                If (nStart <> 0) Begin
                    Constrain Trans.Areanumber eq nStart
                End
                Else Begin
                    If (nStop <> 0) Begin
                        Constrain Trans.Areanumber Le nStop
                    End
                End
            End
        End_Procedure // OnConstrain
    End_Object // oTrans_DD

    Set Main_DD To oTrans_DD


    Object oReport Is A cWebReport
        Procedure OnPageTop
            Integer iArea
            String  sArea sName sManager sEmployer
            //
            Get pnStartAreanumber of oTrans_DD to iArea
            If (iArea <> 0) Begin
                Get ddValueEncode "Areas.AreaNumber" to sArea
                Get ddValueEncode "Areas.Name"       to sName
                Get ddValueEncode "Areas.Manager"    to sManager

                Send WriteHtmlTableBegin 'border="0" width="100%" class="ReportHdr1Table"'
    
                Send WriteHtmlRowBegin
                    Send WriteHtmlCellHeader 'Area Number' 'class="Label"'
                    Send WriteHtmlCell (FormatNumber (sArea,0)) 'class="Data" align="left"'
                    Send WriteHtmlCellHeader 'AreaName' 'class="Label" '
                    Send WriteHtmlCell sName 'class="Data"align="left"'
                    Send WriteHtmlCellHeader 'Area Manager' 'class="Label"'
                    Send WriteHtmlCell sManager 'class="Data" align="left"'
                Send WriteHtmlRowEnd
                Send WriteHtmlTableEnd
            End

            Send WriteHtmlTableBegin 'border="0" width="99%" class="ReportTable"'

            Send WriteHtmlRowBegin
                Send WriteHtmlCellHeader 'Employee ID/Name' 'class="Header" align="left"'
                Send WriteHtmlCellHeader 'Employer'         'class="Header" align="left"'
                Send WriteHtmlCellHeader 'Start Date'       'class="Header" align="left"'
                Send WriteHtmlCellHeader 'Time'             'class="Header" align="left"'
                //Send WriteHtmlCellHeader 'Job#'             'class="Header" align="left"'
                //Send WriteHtmlCellHeader 'Location'         'class="Header" align="left"'
                Send WriteHtmlCellHeader 'Job Information'  'class="Header" align="left"'
                Send WriteHtmlCellHeader 'Area'             'class="Header" align="left"'
                Send WriteHtmlCellHeader 'Equipment'        'class="Header" align="left"'
            Send WriteHtmlRowEnd
        End_Procedure // OnPageTop

        Procedure OnBody 
            Integer iEmployeeID iEquipIdno iAttachIdno
            String  sText sJobDescription
            String  sEmployeeLastname sEmployeeFirstName sEmployer sJobNumber sTransStartdate sTransStarttime 
            String  sCustomerName sLocationName sArea sAreaName sManager sEquipLabel sAttachLabel sAreaDescription
            String  sGPS_Lat_In sGPS_Long_In

            Get DDValueEncode "Trans.EmployeeIdno"      to iEmployeeID
            Get ddValueEncode "Employee.Lastname"       to sEmployeeLastname
            Get DDValueEncode "Employee.Firstname"      to sEmployeeFirstName
            Get DDValueEncode "Employer.Name"           to sEmployer
            Get ddValueEncode "Trans.Startdate"         to sTransStartdate
            Get ddValueEncode "Trans.Starttime"         to sTransStarttime
            Get ddValueEncode "Order.JobNumber"         to sJobNumber
            Get ddValueEncode "Location.Name"           to sLocationName
            Get ddValueEncode "Customer.Name"           to sCustomerName
            Get DDValueEncode "Areas.AreaNumber"        to sArea
            Get DDValueEncode "Areas.Name"              to sAreaName
            Get ddValueEncode "Areas.Manager"           to sManager
            Get ddValueEncode "Trans.EquipIdno"         to iEquipIdno
            Get ddValueEncode "Trans.AttachEquipIdno"   to iAttachIdno
            //GPS Location
            Get ddValueEncode "Trans.GPSLat_In"         to sGPS_Lat_In
            Get ddValueEncode "Trans.GPSLong_In"         to sGPS_Long_In
            
            //Find Equipment Description
            Open Equipmnt
            Clear Equipmnt
            Move iAttachIdno to Equipmnt.EquipIdno
            Find eq Equipmnt.EquipIdno
            Move Equipmnt.Description to sAttachLabel
            Clear Equipmnt
            Move iEquipIdno to Equipmnt.EquipIdno
            Find eq Equipmnt.EquipIdno
            Move Equipmnt.Description to sEquipLabel
            // Build Employee String
            Move (sEmployeeFirstName * sEmployeeLastname * " ( Empl#: " * String(iEmployeeID) *" )" )    to sEmployeeLastname
            // Build Job Description
            Move (sLocationName * " ( Job#" * sJobNumber * ")" )                                         to sJobDescription
//            Move (sTransStartdate * sTransStarttime)       to sTransStartdate
            // Build Area Description
            Move (sAreaName * "-" * sManager * "( Area# " * sArea * " ) " ) to sAreaDescription
            
            // Build Time String
            // https: //www.google.com/maps?z=6&t=m&q=loc:45.25269413+-92.98711180
            
            
            Send WriteHtmlRowBegin
                Get AddRowIdLink sEmployeeLastname to sText
                Get AddGPSLink sGPS_Lat_In sGPS_Long_In sTransStarttime to sTransStarttime
                Send WriteHtmlCell sText                    'class="Data" align="left"'
                Send WriteHtmlCell sEmployer                'class="Data" align="left"'
                Send WriteHtmlCell sTransStartdate          'class="Data" align="left"'
                Send WriteHtmlCell sTransStarttime          'class="Data" align="left"'
                //Send WriteHtmlCell sJobNumber               'class="Data" align="right"'
                //Send WriteHtmlCell sLocationName            'class="Data" align="left"'
                Send WriteHtmlCell sJobDescription          'class="Data" align="left"'
                Send WriteHtmlCell sAreaDescription         'class="Data" align="left"'
                Send WriteHtmlCell (sEquipLabel * sAttachLabel) 'class="Data" align="left"'
            Send WriteHtmlRowEnd
        End_Procedure // OnBody

        Procedure OnPageBottom 
            Send WriteHtmlTableEnd
        End_Procedure // OnPageBottom

        Function RunOpenTransactions Integer iIndex RowId riStartRowId Integer iMaxCount Returns RowId
            RowId   riLastRowId
            Integer eStatus

            Set piOrdering to iIndex
            Set piMaxCount To iMaxCount
            Set piMaxCountBreakLevel To 0
            Set priStartRowId To riStartRowId

            Get DoRunReport To eStatus

            Get priLastRowId To riLastRowId

            Function_Return riLastRowId
        End_Function // RunOpenTransactions
    End_Object // oReport
    
    Function AddGPSLink String sGPS_Lat_In String sGPS_Long_In String sTime Returns String
        If (sGPS_Lat_In <> "0" and sGPS_Long_In <> "0") Begin
            Function_Return ('<a href="https://www.google.com/maps?z=6&t=m&q=loc:'+ sGPS_Lat_In +'+'+ sGPS_Long_In +'" target="_blank">' + sTime + '</a>')
        End
        Else Begin
            Function_Return (sTime)
        End
    End_Function
    
    { Published=True }
    { Description="Sets the reports selection criteria for EmployerIdno" }
    Procedure SetTrans_JobNo String sJobNo
        Set pnJobNo of oTrans_DD to sJobNo
    End_Procedure // SetTrans_EmployerIdno
    
    
    { Published=True }
    { Description="Name to link for drill down report" }
    Procedure SetHRefName String sName
        Set psHRefname of oReport To sName
    End_Procedure // SetHRefName

    { Published=True }
    { Description="Sets the reports selection criteria" }
    Procedure SetTrans_Areanumber String sStart String sStop
        Set pnStartAreanumber of oTrans_DD To sStart
        Set pnStopAreanumber  of oTrans_DD To sStop
    End_Procedure // SetTrans_Areanumber

    { Published=True }
    { Description="Sets the reports selection criteria for EmployerIdno" }
    Procedure SetTrans_EmployerIdno String sEmployer
        Set pnEmployer of oTrans_DD to sEmployer
    End_Procedure // SetTrans_EmployerIdno

    { Published=True }
    { Description="Run Report" }
    Function RunOpenTransactions Integer iIndex String sStartRowId Integer iMaxCount Returns String
        RowId  riStartRowId riEndRowId
        String sEndRowId

        Move 5 to iIndex

        Move (DeserializeRowId(sStartRowId)) to riStartRowId
        Get RunOpenTransactions of oReport iIndex riStartRowId iMaxCount To riEndRowId
        Move (SerializeRowId(riEndRowId)) to sEndRowId

        Function_Return sEndRowId
    End_Function // RunOpenTransactions

    Set pbAllowSaveNew To False
    Set pbAllowSaveEdit To False
    Set pbAllowDelete To False

End_Object // oOpenTransactions
